
struct NotesCollector {
  nextNoteInternalId! int;
  noteInternalIds List<int>;
  internalIdByUserId HashMap<StrSlice, int, StrSliceHasher, StrSliceEquator>;
  userIdByInternalId HashMap<int, StrSlice, IntHasher, IntEquator>;
  noteContentsByInternalId HashMap<int, Section, IntHasher, IntEquator>;
}
fn NotesCollector(nextNoteInternalId int) NotesCollector {
  NotesCollector(
    nextNoteInternalId,
    List<int>(),
    HashMap<StrSlice, int, StrSliceHasher, StrSliceEquator>(
      StrSliceHasher(), StrSliceEquator()),
    HashMap<int, StrSlice, IntHasher, IntEquator>(IntHasher(), IntEquator()),
    HashMap<int, Section, IntHasher, IntEquator>(IntHasher(), IntEquator()))
}
fn Notes(notesCollector NotesCollector) Notes {
  (nextNoteInternalId, noteInternalIds, internalIdByUserId, userIdByInternalId, noteContentsByInternalId) = notesCollector;
  ret Notes(noteInternalIds, noteContentsByInternalId);
}


fn parse(filepath str) Page {
  s = readFileAsString(filepath);
  iter = InputIter(s.slice(), true);
  
  metadata = parseMetadata(&iter);

  notesCollector! = NotesCollector(0);

  pageMembers = List<PageMember>();

  while (iter.hasNext()) {
    iter.skipNewlines();

    if (iter.skipIfNext("<notes/>")) {
      pageMembers.add(Notes(mut notesCollector = NotesCollector(notesCollector.nextNoteInternalId)));
    } else if (iter.nextIs("<notes>")) {
      parseSeparateNotes(&notesCollector, &iter);
      pageMembers.add(Notes(mut notesCollector = NotesCollector(notesCollector.nextNoteInternalId)));
    } else {
      section =
          parseSection(
              &notesCollector,
              &iter,
              &IFunction1<mut, &InputIter, bool>((iter){
                ret iter.hasNext() and not iter.nextIs("\n\n\n");
              }));
      pageMembers.add(section);
    }
  }

  pageMembers.add(
      Notes(
          mut notesCollector = NotesCollector(notesCollector.nextNoteInternalId)));

  ret Page(metadata, pageMembers);
}


fn parseMetadata(input &InputIter) Metadata {
  title! = "".slice();
  subtitle! = "".slice();
  author! = "".slice();
  date! = "".slice();
  path! = "".slice();
  namespace! = "".slice();

  if (input.skipIfNext("---\n")) {
    while (not input.skipIfNext("---\n")) {
      line = input.getUntilExpected("\n");

      maybeAttrNameAndValue = line.splitOnce(":");
      if (maybeAttrNameAndValue.isEmpty()) {
        panic("Invalid attribute line, must have a colon in it! Received:\n" + line);
      }
      (attrName!, attrValue!) = maybeAttrNameAndValue.get();
      mut attrName = attrName.trim();
      mut attrValue = attrValue.trim();

      if (attrName == "title") {
        mut title = attrValue;
      } else if (attrName == "subtitle") {
        mut subtitle = attrValue;
      } else if (attrName == "author") {
        mut author = attrValue;
      } else if (attrName == "date") {
        mut date = attrValue;
      } else if (attrName == "path") {
        mut path = attrValue;
      } else if (attrName == "namespace") {
        mut namespace = attrValue;
      } else {
        panic("Unknown attribute: " + attrName);
      }
    }
  }

  ret Metadata(title, subtitle, author, date, path, namespace);
}


fn nextIsListItem(originalIter &InputIter) bool {
  lookaheadIter = copy(originalIter);
  lookaheadIter.skipNewlines();
  while (lookaheadIter.skipIfNext("   ")) { }
  ret lookaheadIter.nextIs(" * ");
}


fn parseList(
    notesCollector &NotesCollector,
    iter &InputIter,
    continueF &IFunction1<mut, &InputIter, bool>)
UnorderedList {
  rootList = UnorderedList(List<ListMember>());
  // A stack of all the currently open lists.
  openLists = List<&UnorderedList>();
  openLists.add(&rootList);

  while ((continueF)(iter) and iter.nextIsListItem()) {
    iter.skipNewlines();
    thisListItemIndentation! = 1;
    while (iter.skipIfNext("   ")) {
      mut thisListItemIndentation = thisListItemIndentation + 1;
    }
    iter.skipExpect(" * ");

    while (thisListItemIndentation > openLists.len()) {
      newList = UnorderedList(List<ListMember>());
      newListBorrow = &newList;
      openLists.get(openLists.len() - 1).members.add(newList);
      openLists.add(newListBorrow);
    }
    while (thisListItemIndentation < openLists.len()) {
      openLists.remove(openLists.len() - 1);
    }

    lineContents =
        parseLine(
            notesCollector,
            &iter,
            &IFunction1<mut, &InputIter, bool>((iter){
              ret (continueF)(iter) and not iter.nextIs("\n");
            }));
    openLists.get(openLists.len() - 1).members.add(ListItem(lineContents));
  }

  ret rootList;
}


fn parseQuote(
    notesCollector &NotesCollector,
    iter &InputIter,
    continueF &IFunction1<mut, &InputIter, bool>)
Quote {
  inlines = List<InlineNode>();

  while ((continueF)(iter) and iter.nextIs("> ")) {
    iter.skipExpect("> ");

    lineContents =
        parseInlines(
            notesCollector,
            &iter,
            &IFunction1<mut, &InputIter, bool>((iter){
              ret (continueF)(iter) and not iter.nextIs("\n");
            }));
    while (lineContents.inners.len() > 0) {
      inlines.add(lineContents.inners.remove(0));
    }
  }

  ret Quote(Compound(inlines));
}


fn parseSeparateNotes(
    notesCollector &NotesCollector,
    iter &InputIter) {
  iter.skipExpect("<notes>");
  iter.skipNewlines();

  continueF =
      IFunction1<mut, &InputIter, bool>((iter){
        if (not iter.hasNext()) {
          panic("<notes> must end with </notes>");
        }
        ret not iter.nextIs("</notes>");
      });

  while ((continueF)(iter)) {
    iter.skipNewlines();
    iter.skipExpect("#");
    noteId = iter.getUntilExpected(":");

    noteOutput =
        parseSection(
            notesCollector,
            iter,
            &IFunction1<mut, &InputIter, bool>((iter){
              ret (continueF)(iter) and not iter.nextIs("\n\n\n");
            }));
    iter.skipIfNext("\n\n\n");
    notesCollector.addSeparateNote(noteId, noteOutput);
  }

  iter.skipIfNext("</notes>");
}


fn parseInlineNode(
    notesCollector &NotesCollector,
    iter &InputIter)
Opt<InlineNode> {
  if (iter.previousWasBeginOrWhitespace and iter.nextIs("*")) {
    ret Some<InlineNode>(parseBold(notesCollector, iter));
  } else if (iter.previousWasBeginOrWhitespace and iter.nextIs("_")) {
    ret Some<InlineNode>(parseItalic(notesCollector, iter));
  } else if (iter.previousWasBeginOrWhitespace and iter.nextIs("[#")) {
    ret Some<InlineNode>(addInlineNote(notesCollector, iter));
  } else if (iter.previousWasBeginOrWhitespace and iter.nextIs("[")) {
    ret Some<InlineNode>(addLink(notesCollector, iter));
  } else if (iter.previousWasBeginOrWhitespace and iter.skipIfNext("\{")) {
    code = iter.getUntilExpected("\}");
    ret Some<InlineNode>(InlineCode(code));
  } else {
    ret None<InlineNode>();
  }
}

fn parseInlines(
    notesCollector &NotesCollector,
    iter &InputIter,
    continueF &IFunction1<mut, &InputIter, bool>)
Compound {
  inners = List<InlineNode>();

  currentTextNode! StrSlice = "".slice();

  running! = true;
  while (running and iter.hasNext()) {
    if (not (continueF)(iter)) {
      mut running = false;
    } else {
      maybeInner = parseInlineNode(notesCollector, iter);
      if (not maybeInner.isEmpty()) {
        if (currentTextNode != "") {
          inners.add(Text(mut currentTextNode = "".slice()));
        }
        inners.add(maybeInner^.get());
      } else {
        drop(maybeInner);
        mut currentTextNode = (currentTextNode + iter.getNext()).slice();
      }
    }
  }
  if (currentTextNode != "") {
    inners.add(Text(mut currentTextNode = "".slice()));
  }

  ret Compound(inners);
}

fn parseBold(
    notesCollector &NotesCollector,
    iter &InputIter)
Bold {
  iter.skipExpect("*");

  compound =
    parseInlines(
      notesCollector,
      iter,
      &IFunction1<mut, &InputIter, bool>((iter){
        if (not iter.hasNext()) {
          panic("Must end bold!");
          ret false;
        } else if (iter.nextIs("*")) {
          iter.skipExpect("*");
          ret false;
        } else {
          ret true;
        }
      }));

  ret Bold(compound);
}

fn parseItalic(
    notesCollector &NotesCollector,
    iter &InputIter)
Italic {
  iter.skipExpect("_");

  compound =
    parseInlines(
      notesCollector,
      iter,
      &IFunction1<mut, &InputIter, bool>((iter){
        if (not iter.hasNext()) {
          panic("Must end italic!");
          ret false;
        } else if (iter.nextIs("_ ") or iter.nextIs("_.")) {
          iter.skipExpect("_");
          ret false;
        } else if (iter.skipIfEndsWith("_")) {
          ret false;
        } else {
          ret true;
        }
      }));

  ret Italic(compound);
}

fn parseLine(
    notesCollector &NotesCollector,
    iter &InputIter,
    continueF &IFunction1<mut, &InputIter, bool>)
InlineNode {
  ret parseInlines(
    notesCollector,
    iter,
    &IFunction1<mut, &InputIter, bool>((iter){
      ret (continueF)(iter) and iter.hasNext();
    }));
}



fn parseTextUntil(
    iter &InputIter,
    continueF &IFunction1<mut, &InputIter, bool>)
StrSlice {
  code! StrSlice = "".slice();

  running! = true;
  while (running and iter.hasNext()) {
    if (not (continueF)(iter)) {
      mut running = false;
    } else {
      mut code = (code + iter.getNext()).slice();
    }
  }

  ret code;
}


fn parseCodeSnippet(iter &InputIter) CodeSnippet {
  iter.skipExpect("\{\{\{");

  lang = iter.getUntilExpected("\n");

  code =
      parseTextUntil(
          iter,
          &IFunction1<mut, &InputIter, bool>((it){
            if (not it.hasNext()) {
              panic("\{\{\{ must have matching }}}!");
            }
            ret not it.skipIfNext("}}}");
          }));

  ret CodeSnippet(lang, code);
}


fn parseSection(
    notesCollector &NotesCollector,
    iter &InputIter,
    continueF &IFunction1<mut, &InputIter, bool>)
Section {
  sectionMembers = List<SectionMember>();

  while ((continueF)(&iter)) {
    iter.skipNewlines();

    if (iter.nextIs("\{\{\{")) {
      sectionMembers.add(parseCodeSnippet(&iter));
    } else if (iter.nextIs("#")) {
      headerStrength! = 0;
      while (iter.skipIfNext("#")) {
        mut headerStrength = headerStrength + 1;
      }
      headerText =
          iter.parseTextUntil(
              &IFunction1<mut, &InputIter, bool>((it){
                ret iter.hasNext() and not iter.nextIs("\n");
              }));
      sectionMembers.add(Header(headerStrength, headerText.trim()));
    } else if (iter.nextIs(" * ")) {
      sectionMembers.add(
          parseList(
              notesCollector,
              &iter,
              &IFunction1<mut, &InputIter, bool>((iter){
                ret (continueF)(iter) and not iter.nextIs("\n\n");
              })));
    } else if (iter.nextIs("> ")) {
      sectionMembers.add(
          parseQuote(
              notesCollector,
              &iter,
              &IFunction1<mut, &InputIter, bool>((iter){
                ret (continueF)(iter) and not iter.nextIs("\n\n");
              })));
    } else {
      inlineNode =
          parseLine(
              notesCollector,
              &iter,
              &IFunction1<mut, &InputIter, bool>((iter){
                ret (continueF)(iter) and not iter.nextIs("\n\n");
              }));
      sectionMembers.add(Paragraph(inlineNode));
    }
  }

  ret Section(sectionMembers);
}







fn addLink(
    self &NotesCollector,
    iter &InputIter)
Link {
  iter.skipExpect("[");
  linkText = iter.getUntilExpected("]");
  iter.skipExpect("(");
  linkUrl = iter.getUntilExpected(")");
  ret Link(linkText, linkUrl);
}


fn addInlineNote(
    self &NotesCollector,
    iter &InputIter)
NoteAnchor {
  iter.skipExpect("[#");

  internalId = (mut self.nextNoteInternalId = self.nextNoteInternalId + 1);
  self.noteInternalIds.add(internalId);

  // If theres just space after the hash, then we're defining the note's text inline.
  if (iter.skipIfNext(" ")) {
    subNotes = NotesCollector(0);
    subNoteContents =
        parseInlines(
            &subNotes,
            iter,
            &IFunction1<mut, &InputIter, bool>((iter){
              if (not iter.hasNext()) {
                panic("Must end note!");
              }
              ret not iter.skipIfNext("]");
            }));

    println("added inline note");

    paragraph = Paragraph(subNoteContents);
    section = Section(List<SectionMember>());
    section.members.add(paragraph);

    self.noteContentsByInternalId.add(internalId, section);
  } else {
    userNoteId =
        iter.parseTextUntil(
            &IFunction1<mut, &InputIter, bool>((it){
              if (not iter.hasNext()) {
                panic("Must end note!");
              }
              ret not iter.skipIfNext("]");
            }));

    vassert(
        not self.internalIdByUserId.has(userNoteId),
        "Note " + userNoteId + " already exists!");

    println("Adding user note id " + userNoteId);

    self.internalIdByUserId.add(userNoteId, internalId);
    self.userIdByInternalId.add(internalId, userNoteId);
  }

  ret NoteAnchor(internalId);
}

fn addSeparateNote(
    self &NotesCollector,
    noteUserId StrSlice,
    note Section) {
  vassert(
      self.internalIdByUserId.has(noteUserId),
      "Note " + noteUserId + " doesn't exist!");
  internalId = self.internalIdByUserId.get(noteUserId)^.get();


  vassert(
      not self.noteContentsByInternalId.has(internalId),
      "Note " + noteUserId + " already has contents!");

  self.noteContentsByInternalId.add(internalId, note);
}

